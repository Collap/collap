apply plugin: 'maven'

archivesBaseName = 'collap-api'
version = '0.1.1'

/* Dependency specifiers should be declared here. */
ext.tomcatVersion = '8.0.5'
ext.dependencyTomcatJDBC = "org.apache.tomcat:tomcat-jdbc:$tomcatVersion"
ext.dependencyTomcatServlet = "org.apache.tomcat:tomcat-servlet-api:$tomcatVersion"
ext.dependencyEHCache = 'net.sf.ehcache:ehcache:2.8.3'
ext.dependencyHibernate = 'org.hibernate:hibernate-core:4.3.4.Final'
ext.dependencyJade4j = 'collap-jade4j:jade4j:0.4.2'
ext.dependencyHikariCP = 'com.zaxxer:HikariCP:1.3.5'
ext.dependencyYamlbeans = 'com.esotericsoftware.yamlbeans:yamlbeans:1.06'

configurations {
    includedJars
}

dependencies {
    compile dependencyTomcatJDBC
    compile dependencyTomcatServlet
    compile dependencyEHCache
    compile dependencyHibernate
    compile dependencyJade4j
    compile dependencyHikariCP
    compile dependencyYamlbeans

    /* These should be a subset of the compile dependencies. They are copied into the lib folder.
    *  I could not just make compile extend includedJars, as that apparently screws with maven
    *  dependencies for module projects. */
    includedJars dependencyEHCache
    includedJars dependencyHibernate
    includedJars dependencyJade4j
    includedJars dependencyHikariCP
    includedJars dependencyYamlbeans
}

/**
 * Copies the dependencies of the API to a directory to use it in the global tomcat namespace.
 */
task copyDependencies (type: Copy) {
    from configurations.includedJars
    into "$rootDir/../lib"
}

jar {
    destinationDir = file ("$rootDir/../lib")
    manifest {
        attributes 'Implementation-Title': archivesBaseName, 'Implementation-Version': version
    }
}